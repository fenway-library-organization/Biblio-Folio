#!/usr/bin/perl

use warnings;
use strict;

use constant OBJECT => 'Object';
use constant OBJECTS => 'Objects';

use String::Expando;
use Getopt::Long
    qw(:config posix_default gnu_compat require_order bundling no_ignore_case);

sub class;
sub blather;
sub trace;
sub usage;
sub warning;
sub fatal;
sub block;
sub blockf;

(my $prog = $0) =~ s{.*/}{};

my $verbose;
GetOptions(
    'v|verbose' => \$verbose,
) or usage;

my $expando = String::Expando->new;
my $generic = {
    'class' => OBJECT,
    'plural' => OBJECTS,
    'package' => x2pkg(OBJECT),
    'kind' => undef,
    'ttl' => undef,
    'uri' => undef,
    'methods' => {},
};
my $scalar = { 'scalar' => 1 };
my $array = { 'array' => 1 };
my %class = ( OBJECT() => $generic );
my %method;

parse();
build();

# --- Functions

sub parse {
    print STDERR "Parsing...\n";
    my $class;
#my $key;
    while (<STDIN>) {
        next if /^\s*(?:#.*)?$/;  # Skip blank lines and comments
        if (/^\[\*\]$/) {
            # Begin generic block (i.e., stuff to put in Biblio::Folio::Object)
            class "Object";
            $class = $generic;
#undef $key;
        }
        elsif (/^\[(.+)\]$/) {
            # Begin class block
            my ($cls, $pl) = class_sg_pl($1);
            class $cls;
            if (exists $class{$cls}) {
                warning "adding to class: $cls";
                $class = $class{$cls};
                $class->{'plural'} ||= x2kind($pl);
            }
            else {
                $class = $class{$cls} = class_stub($cls);
            }
            if ($cls ne $class->{'class'}) {
                warning "class name mismatch: $cls (defining) != $class->{'class'} (existing)";
                $cls = $class->{'class'};
            }
            if ($verbose) {
                foreach (qw(kind plural package)) {
                    blather "$_: $class->{$_}";
                }
            }
            # fatal "class $cls redefined" if $class{$cls};
            # $class{$cls} ||= $class;
#undef $key;
            define_methods(
                'from' => $generic,
                'to' => $class,
                'property' => x2ref($cls),
                'multi' => 1,
            );
        }
        elsif (/^super(?:class)?\s*=\s*(.+)$/) {
            my $super = trim($1);
            blather "superclass: $super";
            $class->{'super'} = $super;
        }
        elsif (/^(uri(?:\.\w+)?)\s*=\s*(.+)$/) {
            # URI for the class
            my ($k, $v) = ($1, $2);
#undef $key;
            $k =~ s/\.get$//i;
            $k =~ tr/./_/;
            blather "$k: $v";
            $class->{$k} = $v;
        }
        elsif (/^ttl\s*=\s*([0-9]+)$/) {
            # TTL for the class (how long it's cached)
#undef $key;
            my $ttl = $1;
            blather "ttl: $ttl";
            $class->{'ttl'} = $ttl;
        }
        elsif (/^(references)\s*=\s*(.+)$/) {
            # Inbound foreign keys -- i.e., properties (typically found in
            # objects of other classes) that store a UUID for an object in
            # *this* class
#$key = $1;
            my @references;
            foreach my $prop (split /,\s*/, $2) {
                my $multi = 1 if $prop =~ s/\[\]//;
                my $from = $generic;
                if ($prop =~ s/^([^.]+)\.//) {
                    $from = $class{$1} ||= class_stub($1);
                }
                (my $name = $prop) =~ s/Id(s?)$//;
                $name = x2meth($name.($1||''));
                define_methods(
                    'name' => $name,
                    'from' => $generic,
                    'to' => $class,
                    'property' => $prop,
                    'foreign' => 1,
                    'multi' => $multi,
                );
            }
        }
        elsif (/^(method\s*(\w+))\s*=\s*(.+)$/) {
            # Method definition
            my ($name, $spec) = ($2, trim($3));
#($key = $1) =~ s/\s+/ /g;
            blather "method $name";
            my %returns;
            my %meth = ('name' => $name, 'from' => $class, 'spec' => $spec, 'returns' => \%returns);
            my $cls = $class->{'class'};
            my $err = "$cls.$name = $spec";
            if ($spec =~ /^\{$/) {
                my $body = '';
                while (<STDIN>) {
                    fatal "unterminated method spec: $err"
                        if !defined $_;
                    last if /^\}$/;
                    $body .= $_;
                }
                $meth{'body'} = $body;
            }
            elsif ($spec =~ /^{(.+)}$/) {
                $meth{'body'} = trim($1);
            }
            else {
                $spec =~ s/\s+/ /g;
                my ($left, $right) = split / -> /, $spec;
                ($left, my $middle) = split / :: /, $left;
                foreach (split / /, $left) {
                    if (/^(GET|POST|PUT|DELETE)$/) {
                        $meth{'method'} = $_;
                    }
                    elsif (/^((?:Biblio::Folio::)?[A-Z]\w+)(\[\])?$/) {
                        $meth{'multi'} = $2;
                        $meth{'to'} = $class{$1} ||= class_stub($1);
                    }
                    elsif (/^[@]([@\w]+)$/) {
                        $meth{'property'} = $1 eq '@' ? $name : $1;
                        $meth{'custom'} = 1;
                    }
                    elsif (m{^(?:([a-z]+):)?(/\S+)$}) {
                        my $urikey = (defined($1) && $1 ne 'uri') ? 'uri_'.$1 : 'uri';
                        (my $urival = $2) =~ s/{[^{}]+}/%s/;
                        $meth{$urikey} = $urival;
                    }
                    else {
                        fatal "bad method spec: $err";
                    }
                }
                if ($middle) {
                    $meth{'munge'} = [split /\./, $middle];
                    if ($right) {
                        if ($right eq 'scalar') {
                            $meth{'to'} = $scalar;
                        }
                        elsif ($right eq 'array') {
                            $meth{'to'} = $array;
                        }
                        else {
                            fatal "unrecognized return value type: $err";
                        }
                    }
                }
                elsif ($right) {
                    fatal "return value type $right without munging: $err"
                }
                if (!defined $meth{'to'}) {
                    fatal "method return value must be an object, scalar, or array: $err";
                }
                if (!$meth{'to'} && !$meth{'property'}) {
                    fatal "method ${cls}::${name} return value must derive from a property: $_";
                }
            }
            define_methods(%meth);
        }
        elsif (/^data\s*=\s*(.+)/) {
            my $name = trim($1);
            blather "data file base: $name";
            my $kind = $class->{'kind'};
            $class->{'datafile_base'} = $name;
            define_methods(
                'name' => '_datafile_base',
                'from' => $class,
                'body' => sprintf(q{return %s;}, quote($name)),
            );
            define_methods(
                'name' => '_all',
                'from' => $class,
                'body' => sprintf(q{return shift()->site->objects(%s, 'limit' => 1<<31);}, quote($kind)),
            );
        }
        elsif (/^(\w+)\s*=\s*(.+)$/) {
            # Class method; for example:
            #   foo = { my ($self) = @_; return sqrt($self->{'foo'}) }
            #   bar = 999
            #   baz = +{ 'qux' => rand() }
            # Whatever is specified as the value (the bit after "=") is inserted verbatim into the generated code
            my ($k, $v) = ($1, trim($2));
            my $key = '_' . $k;
            blather "class method: $k";
            my $cls = $class->{'class'};
            my $methods = $method{$cls} ||= {};
            my $body;
            if ($v =~ /^{(.*)}$/) {
                $body = trim($1);
                #$methods->{$key} = {
                #    'body' => trim($1),
                #};
            }
            else {
                $body = sprintf("return %s;", $v);  # Note quote($v)
                #$methods->{$key} = {
                #    'body' => sprintf("return %s;", quote($v)),
                #};
            }
            define_methods(
                'name' => $key,
                'from' => $class,
                'body' => $body,
            );
        }
    }
    print STDERR "Parsing complete\n\n";
}

sub define_methods {
    my %arg = @_;
    my ($from, $name, $body) = @arg{qw(from name body)};
    my $methods = $method{$from->{'class'}} ||= {};
    if ($body) {
        $methods->{$name} = \%arg;
        return;
    }
    my %define;
    my $to = $arg{'to'} ||= {};
    my $prop = $arg{'property'};
    my $multi = $arg{'multi'};
    my $custom = $arg{'custom'};
    my $foreign = $arg{'foreign'};
    my ($to_cls, $to_pl, $to_pkg, $to_kind) = @$to{qw(class plural package kind)};
    if ($custom) {
        $define{$name} = \%arg;
    }
    else {
        my $xref_sg = x2ref($prop || $to_cls);
        my $xref_pl = x2refs($prop || $to_cls);
        if ($to->{'class'}) {
            # my $instance = $holding->instance;
            my $meth_sg = $foreign ? x2meth($name) : x2meth($to_cls);
            $define{$meth_sg} = {
                'property' => $xref_sg,         # holdingsRecordId
                'returns' => $to,
            };
            if ($multi) {
                warning "foreign and multi!? $from->{class}.$name"
                    if $foreign;
                my $meth_pl = x2meth($to_pl);
                $define{$meth_pl} = {
                    'property' => $xref_pl,     # institutionIds
                    'returns' => $to,
                };
            }
        }
        if (defined $prop) {
            my $meth = x2meth($prop);
            # my $id = $holding->instance_id;   -- e.g., if we don't want to fetch the referenced object
            $define{$meth} = {
                'property' => $xref_sg,     # instanceId
                'returns' => $scalar,
            };
            if ($multi) {
                # my @ids = $instance->contributor_ids();
                $meth .= 's';
                $define{$meth} = {
                    'property' => $xref_pl, # contributorIds
                    'returns' => $array,
                };
            }
        }
        elsif (0) {
            # my $metadata = $instance->metadata;
            warning "can this even happen???";
            my $meth_sg = x2meth($to_cls);
            $define{$meth_sg} = {
                'property' => $xref_sg,
                'returns' => $to,
            };
            if ($multi) {
                # my @holdings = $instance->holdings;
                my $meth_pl = x2meth($to_pl);
                $define{$meth_pl} = {
                    'property' => $xref_pl,
                    'returns' => $to,
                };
            }
        }
    }
    while (my ($m, $method) = each %define) {
        my $from_cls = $from->{'class'};
        my $existing_method = $methods->{$m};
        if ($existing_method) {
            my $props = $existing_method->{'properties'} ||= [ delete $existing_method->{'property'} ];
            my $new_prop = $method->{'property'};
            if (!defined $new_prop) {
                warning "oddball duplicate method $from_cls.$m";
            }
            elsif (grep { $_ eq $new_prop } @$props) {
                warning "duplicate property $new_prop for $from_cls.$m";
            }
            else {
                push @$props, $new_prop;
            }
        }
        else {
            $methods->{$m} = {
                'name' => $m,
                %$method,
            };
        }
    }
}

sub class_sg_pl {
    my ($cls) = @_;
    my $pl;
    if ($cls =~ s{/(.+)/(.+)}{$1}) {
        $pl = $1;
    }
    elsif ($cls =~ s{/(.+)}{}) {
        $pl = $cls . $1;
    }
    elsif ($cls =~ s{/$}{}) {
        $pl = $cls;
    }
    else {
        $pl = $cls . 's';
    }
    return ($cls, $pl);
}

sub class_stub {
    my ($cls, $pl) = class_sg_pl(@_);
    return {
        'class' => $cls,
        'plural' => x2kind($pl),
        'package' => x2pkg($cls),
        'kind' => x2kind($cls),
    };
}

sub build {
    print STDERR "Building code...\n";
    block <<'EOS';
use strict;
use warnings;
EOS
    block <<'EOS';
use Biblio::Folio::Object;
EOS
    my $object = delete $class{OBJECT()};
    foreach my $class ($object, sort { $a->{'package'} cmp $b->{'package'} } values %class) {
        block <<'EOS';
# ------------------------------------------------------------------------------
EOS
        my $pkg = $class->{'package'};
        my $cls = x2cls($pkg);
        class $cls;
        my ($ttl, $uri) = @$class{qw(ttl uri)};
        $uri =~ s/{[^{}]+}/%s/ if defined $uri;
        printf qq{package %s;\n}, $pkg;
        if ($cls ne OBJECT) {
            my $super = x2pkg($class->{'super'} || OBJECT);
            blather "superclass: $super";
            printf qq{\n\@%s::ISA = qw(%s);\n}, $pkg, $super;
        }
        printf qq{\nsub _ttl { %s }}, quote($ttl) if defined $ttl;
        if (defined $uri) {
            blather "uri: $uri";
            printf qq{\nsub _uri { %s }}, quote($uri);
            my $uri_search = $class->{'uri_search'};
            if (!defined $uri_search && $uri =~ m{(.+)/%s$}) {
                $class->{'uri_search'} = $1;
            }
        }
        foreach (grep { /^uri_/ } keys %$class) {
            my $uri2 = $class->{$_};
            blather "$_: $uri2";
            printf qq{\nsub _%s { %s }}, $_, quote($uri2);
        }
        print qq{\n\n};
        my $methods = $class->{'methods'} = $method{$cls} || {};
        foreach my $m (sort keys %$methods) {
            blather "method $m";
            my $method = $methods->{$m};
            my ($prop, $props, $mmeth, $muri, $body, $returns, $munge) = @$method{qw(property properties method uri body returns munge)};
            my $propeval = prop_evaluator($prop || $props);
            my ($rkind, $rcls) = @$returns{qw(kind class)};
            my $sfx = '';
            if ($munge) {
                $sfx = sprintf q{, 'munge' => [%s]}, join(', ', map { quote($_) } @$munge);
                blather "  munge:", join('.', @$munge);
            }
            if (defined $body) {
                # Arbitrary code
                my @body = split /\n/, $body;
                $body = join("\n", map { '    ' . $_ } @body);
                my $bodytrunc = $body[0];
                $bodytrunc .= ' ...' if @body > 1;
                blather "  body: $bodytrunc";
                blockf <<'EOS', $m, $body;
sub %s {
%s
}
EOS
            }
            elsif (defined $rcls) {
                # Return an object
                blather "  returns object: $class{$rcls}{'class'}";
                my $rttl = $class{$rcls}{'ttl'};
                my $accessor = $rttl ? 'cached' : 'object';
                if (defined $muri) {
                    blather "    from uri: $muri";
                    blockf <<'EOS', $m, $accessor, $rkind, $propeval, quote($muri), $sfx;
sub %s {
    my ($self) = @_;
    return $self->site->%s('%s', 'id' => %s, 'uri' => %s%s);
}
EOS
                }
                else {
                    blockf <<'EOS', $m, $accessor, $rkind, $propeval, $sfx;
sub %s {
    my ($self) = @_;
    return $self->site->%s('%s', %s%s);
}
EOS
                }
            }
            elsif (defined $muri) {
                blather "  returns non-object from uri: $muri";
                # Return something that isn't an object
                blockf <<'EOS', $m, $pkg, $propeval, quote($muri), $sfx;
sub %s {
    my ($self) = @_;
    return $self->site->fetch('%s', 'id' => %s, 'uri' => %s, 'scalar' => 1%s);
}
EOS
            }
            else {
                fatal "can't munge a bare property accessor: $cls->$m"
                    if defined $munge;
                blather "  return literal: $propeval";
                blockf <<'EOS', $m, $propeval;
sub %s {
    my ($self) = @_;
    return %s;
}
EOS
            }
        }
        # Pick up any hand-written code
        printf qq{eval q{use %s};\n\n}, $pkg;
    }
    # Build Biblio::Folio::Classes methods
    class('Classes');
    my (@all, %datafile);
    foreach (sort keys %class) {
        my $class = $class{$_};
        my $pkg = $class{$_}{'package'};
        my $cls = $class{$_}{'package'};
        fatal "no package for class $_" if !defined $pkg;
        push @all, $pkg;
        my $datafile_base = $class->{'datafile_base'}
            or next;
        blather "data file: $cls -> $datafile_base";
        $datafile{$pkg} = $class->{'datafile_base'};
    }
    block <<'EOS';
# ------------------------------------------------------------------------------
EOS
    print "package Biblio::Folio::Classes;\n\n";
    printf <<'EOS', join("\n", map { '        ' . $_ } @all);
sub all {
    return qw(
%s
    );
}
EOS
    print "\n";
    printf <<'EOS', join("\n", map { sprintf('        %-44s %s', $_, $datafile{$_}) } sort keys %datafile);
sub datafiles {
    return qw(
%s
    );
}
EOS
    print "\n1;\n";
    print STDERR "Code complete\n\n";
}

sub prop_evaluator {
    my ($props) = @_;
    if (ref $props) {
        return '(' . join(' // ', map { sprintf q{$self->{'%s'}}, $_ } @$props) . ')';
    }
    elsif (!defined $props) {
        return 'undef';
    }
    else {
        return sprintf q{$self->{'%s'}}, $props;
    }
}

sub block {
    print @_, "\n";
}

sub blockf {
    my $fmt = shift;
    printf $fmt, @_;
    print "\n";
    #printf(@_), "\n";
}

sub trim {
    local $_ = shift;
    s/^\s+|\s+$//g;
    return $_;
}

sub camel {
    local $_ = shift;
    s/[-_\s]+(.)/\U$1/g;
    return $_;
}

sub uncamel {
    local $_ = shift;
    s/(?<=[a-z])(?=[A-Z])/_/g;
    return lc $_;
}

sub quote {
    my $str = shift;
    return 'undef' if !defined $str;
    fatal sprintf 'quote(%s)', ref($str) if ref($str);
    return $str if $str =~ /^-?[0-9]+$/;      # Integers
    return qq{'$str'} if $str !~ /[\\']/;     # Good for URLs and most other strings
    $str = qq{"\Q$str\E"};                    # Let Perl do the escaping
    $str =~ s{(\\\\)|\\/}{$1 ? $1 : '/' }eg;  # Remove pointless escaping of forward slashes
    return $str;
}

sub x2pkg {
    my ($x) = @_;
    return $x if $x =~ /^Biblio::Folio/;
    return kind2pkg($x) if $x =~ /^[a-z_]+$/;
    return kind2pkg(pkg2kind($x));
}

sub x2kind {
    my ($x) = @_;
    $x =~ s/.+:://;
    return pkg2kind(kind2pkg($x));
}

sub x2meth {
    goto &x2kind;
}

sub x2ref {
    my ($x) = @_;
    $x =~ s/^Biblio::Folio:://;
    $x = lcfirst camel($x);
    $x =~ s/Id$//;
    return $x . 'Id';
}

sub x2refs {
    my ($x) = @_;
    return x2ref($x) . 's';
}

sub x2cls {
    my ($x) = @_;
    $x = x2pkg($x);
    $x =~ s/^Biblio::Folio:://;
    return $x;
}

sub kind2pkg {
    my ($kind) = @_;
    return 'Biblio::Folio::' . ucfirst camel($kind);
}

sub pkg2kind {
    my ($pkg) = @_;
    $pkg =~ s/^Biblio::Folio:://;
    return lcfirst uncamel($pkg);
}

sub class {
    print STDERR @_, "\n";
}

sub blather {
    print STDERR "  . @_\n" if $verbose;
}

sub usage {
    print STDERR "usage: $prog [-v] < classes.ini";
}

sub warning {
    print STDERR "  ! @_\n";
}

sub fatal {
    print STDERR "$prog: @_\n";
    exit 2;
}

1;

__END__
### elsif (/^blessed\s+references\s*=\s*(.+)$/) {
###     foreach my $p (split /,\s*/, $1) {
###         my $prop = $p;
###         my $from_cls = OBJECT;
###         $from_cls = ucfirst camel($1)
###             if $prop =~ s/^([^.]+)\.//;
###         my $methods = $method{$from_cls} ||= {};
###         my $meth = x2meth($prop);
###         #warning "redefined reference from $from_cls to $cls via $meth"
###         #    if exists $methods->{$meth};
###         $methods->{$meth} = {
###             'class' => $class,
###             'method' => $prop,
###             'properties' => [],
###             'multi' => $multi,
###             'bless' => 1,
###         };
###         push @{ $methods->{$meth}{'properties'} }, $p;
###     }
### }

            foreach my $p (split /,\s*/, $2) {
                my $multi = $p =~ s/\[\]$//;
                my $from_cls = OBJECT;
                $from_cls = x2cls($1)
                    if $p =~ s/^([^.]+)\.//;
                my $prop = x2ref($p);
                my $meth = x2meth($p);
                my $methods = $method{$from_cls} ||= {};
                # warning "redefined reference from $from_cls to $cls via $meth"
                #     if exists $methods->{$meth};
                $methods->{$meth} ||= {
                    'name' => $meth,
                    'property' => $p,
                    'returns' => $class,
                    'multi' => $multi,
                };
            }

                while ($spec =~ /\S/) {
                    $spec =~ s/^\s+//;
                    if ($spec =~ s/^\[\]//) {
                        $meth{'multi'} = 1;
                    }
                    elsif ($spec =~ s/^\.([\w.]+)//) {
                        $meth{'munge'} = [split /\./, $1];
                    }
                    elsif ($spec =~ s{^(/\S+)}{}) {
                        ($meth{'uri'} = $1) =~ s/{[^{}]+}/%s/;
                    }
                    elsif ($spec =~ s/^\((\w+)\)//) {
                        $meth{'property'} = $1;
                    }
                    else {
                        die "unparseable: $err";
                    }
                }
